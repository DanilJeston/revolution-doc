---
title: 第一个简单的脚本
date: 2021-05-10 13:33:42
permalink: /pages/91995e/
---

# 第一个简单的脚本

::: danger 警告！
本文编辑时的 DDLC 中文 Mod 模板可能与教程有极大出入，您应该在文档和模板完善后再来查看本文档。
:::

在 Ren'Py 和 DDLC 的世界里，剧本与视觉内容都围绕着代码展开 —— 但不必慌张！ Ren'Py 的语法已经做到了极致的简约，不用太多理解成本，上手即可。

## 从 script.rpy 开始
开始之前，你应该先来看一下 `script.rpy`，里面包含了串联起整个游戏的逻辑代码，跟着注释食用更佳。

```renpy
# Script.rpy
# 这里支撑着游戏的整体运行逻辑。

label start:

    # 用于防作弊（读取之前存档）的 ID。
    # 不要在这里修改相应 ID，请在 definitions.rpy 修改。
    $ anticheat = persistent.anticheat

    # 这里控制游戏的章节，对于 poem game 有用。
    $ chapter = 0

    # 如果游戏在暂停时被玩家退出，那么它将设为 False。
    # 慎用。
    $ _dismiss_pause = config.developer

    # 角色命名。
    # 如需添加新角色 -> $ mi_name = "Mike"
    # 一定要记得去 definitions.rpy 再定义一次！
    $ s_name = "Sayori" # 可选译名：纱世里（推荐）、莎世里、纱悠里
    $ m_name = "Monika" # 推荐译名：莫妮卡
    $ n_name = "Natsuki" # 可选译名：夏树（推荐）、娜苏琪
    $ y_name = "Yuri" # 推荐译名：优里

    # 控制是否显示底部文字菜单和是否允许使用 Esc 显示菜单。
    $ quick_menu = True

    # 控制对话文字风格。
    # 对于“修改”类文本，请使用 'style.edited'，否则请保持 'style.normal'
    $ style.say_dialogue = style.normal

    # 控制 Sayori 此时是否 GG，但这个变量目前没什么用。
    $ in_sayori_kill = None
    
    # 控制是否允许玩家跳过 / 快进对话。
    $ allow_skipping = True
    $ config.allow_skipping = True

    # 脚本开始
    call ch1_start

    return

# 游戏的 END 标志。（不是 Credits）
label endgame(pause_length=4.0):
    $ quick_menu = False
    stop music fadeout 2.0
    scene black
    show end
    with dissolve_scene_full
    pause pause_length
    $ quick_menu = True
    return
```

## 着手你的第一个 Label

### Next 分支

如果你使用 Next 分支，那么现在您不必再理会 `script.rpy` 文件，直接打开 `script-ch1.rpy`，然后保留文件第一行的 `label ch1_start:`，删除其他内容即可。

### 旧分支
如果你使用旧分支，那么准备工作会复杂一点点。将 `scrpit.rpy` 中被注释包围的几行改成：

```renpy
    call ch1_start
```

::: tip 提示
此处是为了与 Next 分支模板保持一致。
:::

然后再创建文件 `script-ch1.rpy`（包含后缀），再在新文件中输入：

```renpy
label ch1_start:
```

第一个 Label 就已经创建完成了。

## 开始让角色说话

没有文字的视觉小说可能连小说都算不上，它总得有角色说话。而对于 DDLC，总得有角色出演吧。

现在，我们从旁白和 Monika 开始添加对话吧。另起一行，按 4 个空格（VSCode 之类的编辑器可以直接按 Tab 键），输入：

```renpy
    "今天是学园祭的第一天。"
    m "好了，各位！我们开始准备吧！"
```

::: warning 注意！
包住文字的引号必须使用英文符号 `""`。
:::

接下来，运行工程，你应该可以看到两句对话，一句是旁白，一句来自 Monika。这就是一段最基础的对话。

理解了这些基础内容后，我们可以添加更多对话与角色，让 Mod 更加热闹一点点。在这里，我先来提示一下 Ren'Py 的基础语法：

```renpy
    <chr> "内容"
```

在 DDLC Mod 里，你可以在 `<chr>` 里输入以下内容：

| `<chr>` | 代表的角色 |
|-|-|
|`m`  |Monika |
|`s`  |Sayori  |
|`n`  |Natsuki  |
|`y`  |Yuri  |
|`mc` |主角（你）  |
|不填  |旁白  |

现在，尝试理解下面的代码，并运行一下：

```renpy
    "今天是学园祭的第一天。"
    m "好了，各位！我们开始准备吧！"
    s "耶！小饼干！"
    n "要不也来尝下我的纸杯蛋糕？"
    y "...其实都可以了。"
    mc "总之，以后也要多多关照！"
```

## 添加 BGM
现在，Mod 已经拥有了对话，但还缺了许多东西，比如 BGM 与背景，这里我们先从 BGM 开始。